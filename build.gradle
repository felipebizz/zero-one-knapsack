plugins {
  id 'java'
  id 'application'
}

def javaVersion = '1.8'
def defaultEncoding = 'UTF-8'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) { options.encoding = defaultEncoding }
tasks.withType(Javadoc) { options.encoding = defaultEncoding }

group = 'com.mobiquityinc'
version = '1.0-RELEASE'
mainClassName = 'com.mobiquityinc.Main'

jar {
  baseName = 'pcaker'
  manifest {
    attributes provider: 'gradle'
    attributes 'Main-Class': mainClassName
    attributes version: version
  }
}
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives sourcesJar
  archives javadocJar
}

repositories {
  mavenCentral()
}

ext {
  log4jVersion = '1.2.17'
  powermockVersion = '1.7.0'
}

dependencies {
  compile group: 'log4j', name: 'log4j', version: "${log4jVersion}"
  testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: "${powermockVersion}"
  testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: "${powermockVersion}"
}
